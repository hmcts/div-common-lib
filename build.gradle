plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'java-library'
    id 'maven-publish'
}

def buildNumber = System.getenv("RELEASE_VERSION")?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
version buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

checkstyle {
    toolVersion = '10.25.0'
    maxWarnings = 57
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/div-common-lib"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/div-common-lib"
    }
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'div-common-lib'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Divorce common lib')
                root.appendNode('name', 'Divorce common lib')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

ext {
    lombokVersion = '1.18.38'
    springCloudVersion = 'Greenwich.SR2'
    feignVersion = '10.2.3'
    feignFormVersion = '3.5.0'
    springfoxSwagger = '2.9.2'
    springVersion = '2.5.15'
    mapstruct = '1.2.0.Final'
}

ext["spring-cloud-openfeign.version"] = "2.1.2.RELEASE"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    api group: 'io.github.openfeign', name: 'feign-jackson', version: feignVersion
    api group: 'io.github.openfeign', name: 'feign-httpclient', version: feignVersion
    api group: 'io.github.openfeign.form', name: 'feign-form', version: feignFormVersion
    api group: 'io.swagger', name: 'swagger-annotations', version: '1.5.20'
    api group: 'org.mapstruct', name: 'mapstruct-jdk8', version: mapstruct

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.13.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.13.0'
    testImplementation("org.junit.vintage:junit-vintage-engine") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
    }

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springVersion

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path-assert', version: '2.9.0'

    implementation group: 'com.deliveredtechnologies', name: 'rulebook-core', version: '0.9.1'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.0.Final'
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: springfoxSwagger
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: springfoxSwagger
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'

    compileOnly group: 'org.mapstruct', name: 'mapstruct-processor', version: mapstruct
    annotationProcessor("org.mapstruct:mapstruct-processor:${mapstruct}")
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

jar {
    enabled = true
}
